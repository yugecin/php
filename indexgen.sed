# give id's to each <h1-6> and save a list of them in the file 'INDEX'

# every h has to be on it's own line for this to work

/^<h[1-6]>.*<\/h[1-6]>$/ {
	h
	s_^<h[1-6]>\(.*\)</h[1-6]>$_\1_
	s/^\([^<]*\)<.*$/\1/
	s/[^a-zA-Z0-9\-_.]//g
	s/$/~END~/
	G
	h
	/<\/h1>$/ { s/^/~T~/ }
	/<\/h2>$/ { s/^/.... ~T~/ }
	/<\/h3>$/ { s/^/........ ~T~/ }
	/<\/h4>$/ { s/^/............ ~T~/ }
	/<\/h5>$/ { s/^/................ ~T~/ }
	/<\/h6>$/ { s/^/.................... ~T~/ }
	s_^\(.*\)~T~\(.*\)~END~\n<h.>\([^<]*\) \?<.*$_\1<a href="#\2">\3</a><br/>_w INDEX
	g
	s_^\(.*\)~END~\n<h\([1-6]\)>\(.*\)$_<h\2 id="\1">\3_
}

